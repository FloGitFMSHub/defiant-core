import("//brave/components/common/typescript.gni")
import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(enable_extensions)

pack_web_resources("generated_resources") {
  resource_name = "dissenter_extension"
  output_dir = "$root_gen_dir/brave/components/dissenter_extension"
  # need wepack and GRD generation first
  deps = [
    "extension/dissenter_extension"
  ]
}

grit("static_resources") {
  source = "extension/resources.grd"

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  outputs = [
    "grit/dissenter_extension.h",
    "grit/dissenter_extension_resources_map.cc",
    "grit/dissenter_extension_resources_map.h",
    "dissenter_extension_static.pak",
  ]

  deps = [
    "extension/dissenter_extension",
  ]

  resource_ids = "//brave/browser/resources/resource_ids"
  output_dir = "$root_gen_dir/brave/components/dissenter_extension"
}

repack("resources") {
  deps = [
    ":static_resources",
    ":generated_resources",
  ]

  sources = [
    "$root_gen_dir/brave/components/dissenter_extension/dissenter_extension_static.pak",
    "$root_gen_dir/brave/components/dissenter_extension/dissenter_extension_generated.pak",
  ]

  output = "$root_gen_dir/brave/components/dissenter_extension/dissenter_extension_resources.pak"
}
